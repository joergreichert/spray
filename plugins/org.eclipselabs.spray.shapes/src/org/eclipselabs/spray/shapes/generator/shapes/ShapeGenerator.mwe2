module org.eclipselabs.spray.shapes.generator.shapes.ShapeGenerator

import org.eclipse.emf.mwe.utils.*

var srcGenDir = "src-gen"
var srcManDir = "src"
var modelPath

Workflow {
    bean = StandaloneSetup {
        registerGeneratedEPackage = "org.eclipse.xtext.xbase.XbasePackage"
        registerGeneratedEPackage = "org.eclipselabs.spray.styles.styles.StylesPackage"
        registerGeneratedEPackage = "org.eclipselabs.spray.shapes.ShapesPackage"
    }

    component = org.eclipse.xtext.ecore.EcoreSupport{}

	component = org.eclipselabs.spray.xtext.util.ReaderExt {
		// lookup all resources on the classpath
		// useJavaClassPath = true

		// or define search scope explicitly
		path = modelPath

		// this class will be generated by the xtext generator 
		register = org.eclipselabs.spray.shapes.ShapeStandaloneSetup {}
    	uriFilter = org.eclipse.xtext.mwe.NameBasedFilter {
    		regularExpression = ".*\\.style|.*\\.shape"
    	}
		loadResource = {
			slot = "model"
			uri = ".*\\.style|.*\\.shape" 
		}
	}

	component = DirectoryCleaner {
		directory = srcGenDir
	}
	
	component = org.eclipse.xtext.generator.GeneratorComponent {
		register = org.eclipselabs.spray.styles.StyleStandaloneSetup {}
		slot = 'model'
		outlet = {
			path = srcGenDir
		}
		outlet = {
		    outletName ="SRCMAN"
			path = srcManDir
		}
	}	

	component = org.eclipse.xtext.generator.GeneratorComponent {
		register = org.eclipselabs.spray.shapes.ShapeStandaloneSetup {}
		slot = 'model'
		outlet = {
			path = srcGenDir
		}
		outlet = {
		    outletName ="SRCMAN"
			path = srcManDir
		}
	}
}
